include(CheckCXXCompilerFlag)

if(CMAKE_CXX_COMPILER_ID MATCHES "MSVC")
    set(OPTIONS /W4 /constexpr:steps1000000000)
    check_cxx_compiler_flag(/permissive HAS_PERMISSIVE_FLAG)
    if(HAS_PERMISSIVE_FLAG)
        set(OPTIONS ${OPTIONS} /permissive-)
    endif()

    check_cxx_compiler_flag(/std:c++latest HAS_CPPLATEST_FLAG)
    check_cxx_compiler_flag(/std:c++20 HAS_CPP20_FLAG)
elseif(CMAKE_CXX_COMPILER_ID MATCHES "GNU|Clang")
    set(CMAKE_VERBOSE_MAKEFILE ON)
    if (CMAKE_CXX_COMPILER_ID MATCHES "GNU")
        set(OPTIONS -Wall -Wextra -Wpedantic -fconstexpr-ops-limit=1000000000)
    else()
        set(OPTIONS -Wall -Wextra -Wpedantic -fconstexpr-steps=1000000000)
    endif()

    check_cxx_compiler_flag(-std=c++2a HAS_CPP2A_FLAG)
    check_cxx_compiler_flag(-std=c++20 HAS_CPP20_FLAG)
endif()

function(make_test target std sources)
    add_executable(${target} ${sources})
    target_compile_options(${target} PRIVATE ${OPTIONS})
    target_include_directories(${target} PRIVATE 3rdparty/Catch2)
    target_link_libraries(${target} PRIVATE ${CMAKE_PROJECT_NAME})
    set_target_properties(${target} PROPERTIES CXX_EXTENSIONS OFF)
    if(std)
        if(CMAKE_CXX_COMPILER_ID MATCHES "MSVC")
            target_compile_options(${target} PRIVATE /std:${std})
        else()
            target_compile_options(${target} PRIVATE -std=${std})
        endif()
    endif()
    add_test(NAME ${target} COMMAND ${target})
endfunction()

if(HAS_CPP20_FLAG)
    make_test(constexpr_integral-cpp20.t c++20 test_constexpr_integral.cpp)
elseif(HAS_CPP2A_FLAG)
    make_test(constexpr_integral-cpp2a.t c++2a test_constexpr_integral.cpp)
elseif(HAS_CPPLATEST_FLAG)
    make_test(constexpr_integral-cpplatest.t c++latest test_constexpr_integral.cpp)
endif()
